( rm -f etc/slib.rom && uxncli ~/roms/uxnlin.rom etc/slib.tal && uxnasm etc/slib.tal etc/slib.rom && uxncli etc/slib.rom )

|0100 


	;a ;a ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '1 #18 DEO #0a18 DEO
	;a ;b ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;a ;c ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;a ;d ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO

	;b ;a ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;b ;b ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '1 #18 DEO #0a18 DEO
	;b ;c ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;b ;d ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO

	;c ;a ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;c ;b ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;c ;c ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '1 #18 DEO #0a18 DEO
	;c ;d ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO

	;d ;a ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;d ;b ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;d ;c ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '0 #18 DEO #0a18 DEO
	;d ;d ;scmp JSR2 ;print/byte JSR2 #2018 DEO LIT '1 #18 DEO #0a18 DEO


	#010f DEO

BRK

@a "This 20 "is 20 "the 20 "first 20 "string. $1
@b "This 20 "is 20 "the 20 "second 20 "string. $1
@c "this 20 "is $1
@d $1

@scmp3 ( a[3]* b[3]* -- flag )

	LDA2k STH2 INC2 INC2 SWP2
	LDA2k STH2 INC2 INC2 EQU2r
	LDA STH LDA STH EQUr
	ANDr STHr

JMP2r

@sihx ( str* -- flag )

	&while
		LDAk ,chex JSR #ff NEQ ,&valid JCN
			POP2 #00 JMP2r &valid
		INC2 LDAk ,&while JCN
	POP2
	#01

JMP2r

@chex ( char -- value/ff )

	DUP #2f GTH OVR #3a LTH AND ,&number JCN
	DUP #60 GTH OVR #67 LTH AND ,&lc JCN
		POP #ff

JMP2r
	&number #30 SUB JMP2r
	&lc #57 SUB JMP2r

@scap ( str* -- end* ) LDAk #00 NEQ JMP JMP2r &w INC2 LDAk ,&w JCN JMP2r
@spop ( str* -- ) LDAk ,&n JCN POP2 JMP2r &n ,scap JSR #0001 SUB2 #00 ROT ROT STA JMP2r
@sput ( chr str* -- ) ,scap JSR STA JMP2r
@slen ( str* -- len* ) DUP2 ,scap JSR SWP2 SUB2 JMP2r
@scat ( src* dst* -- ) DUP2 ,slen JSR ADD2
@scpy ( src* dst* -- ) STH2 &w LDAk STH2kr STA INC2r INC2 LDAk ,&w JCN POP2 #00 STH2r STA JMP2r
@scmp ( a* b* -- f ) STH2 &l LDAk LDAkr STHr ANDk #00 EQU ,&e JCN NEQk ,&e JCN POP2 INC2 INC2r ,&l JMP &e NIP2 POP2r EQU JMP2r
@mclr ( src* len* -- ) OVR2 ADD2 SWP2 &l STH2k #00 STH2r STA INC2 GTH2k ,&l JCN POP2 POP2 JMP2r
@mcpy ( src* dst* len* -- ) SWP2 STH2 OVR2 ADD2 SWP2 &loop LDAk STH2kr STA INC2r INC2 GTH2k ,&loop JCN POP2 POP2 POP2r JMP2r


@print ( short* -- )

	SWP ,&byte JSR
	&byte ( byte -- ) DUP #04 SFT ,&char JSR
	&char ( char -- ) #0f AND DUP #09 GTH #27 MUL ADD #30 ADD #18 DEO

JMP2r
