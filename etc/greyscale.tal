( Drifblim )

|0010 @Console &vector $2 &read $1 &pad $5 &write $1 &error $1
|00a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0000

	@src $40

|0100

	;on-console .Console/vector DEO2

BRK

@on-console ( -> )

	.Console/read DEI
	DUP #20 LTH ,&end JCN
	DUP #7f GTH ,&end JCN
	;src ;slen JSR2 #003f GTH2 ,&end JCN
		;src ROT ;sput JSR2 BRK
		&end
	POP

	( load src )
	;src .File/name DEO2
	#f000 ;data SUB2 .File/length DEO2
	;data .File/read DEO2

	( set size )
	.File/success DEI2
	;sqrt JSR2 STH2k SWP 
		DUP2 ;header/width STA2
		;header/height STA2

	( save header )
	;dst .File/name DEO2
	#0012 .File/length DEO2
	;header .File/write DEO2

	( draw pixels )
	
	#0001 .File/length DEO2
	STH2r DUP2 MUL2
	;data STH2
	#0000
	&loop
		STH2kr LDA ;pixel STA INC2r
		;pixel .File/write DEO2
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	POP2r

	( debug ) #010e DEO
	( halt ) #010f DEO

BRK

@pixel 00

@dst "bin/output.tga $1

@header
	( type  ) 00 00 03
	( specs ) 00 00 00 00 00
	( x y   ) 00 00 00 00
	( size  ) &width $2 &height $2
	( depth ) 08 20

( helpers )

@sqrt ( v* -- r* )

	STH2 #0003 
	&while 
		INC2 DUP2k MUL2 STH2kr LTH2 ,&while JCN 
	POP2r

JMP2r

@slen ( str* -- len* )

	DUP2 ,scap JSR SWP2 SUB2

JMP2r

@scap ( str* -- str-end* )

	LDAk #00 NEQ JMP JMP2r
	&while INC2 LDAk ,&while JCN

JMP2r

@sput ( str* char -- )

	ROT ROT ,scap JSR STA

JMP2r

( buffers )

@data
