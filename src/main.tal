( Drifblim )

|00 @System     &vector $2 &wst      $1 &rst    $1 &eaddr  $2 &ecode  $1 &pad     $1 &r       $2 &g      $2 &b     $2 &debug  $1 &halt $1
|10 @Console    &vector $2 &read     $1 &pad    $5 &write  $1 &error  $1
|a0 @File       &vector $2 &success  $2 &stat   $2 &delete $1 &append $1 &name    $2 &length  $2 &read   $2 &write $2

%EMIT { #18 DEO }
%HALT { #010f DEO BRK }
%PRINT  { ;print-str JSR2 #0a EMIT }
%DEBUG  { ;print/byte JSR2 #0a EMIT }
%DEBUG2 { ;print/short JSR2 #0a EMIT }
%RTN { JMP2r }

%+  { ADD }  %-  { SUB }  %*  { MUL }  %/  { DIV }
%<  { LTH }  %>  { GTH }  %=  { EQU }  %!  { NEQ }
%++ { ADD2 } %-- { SUB2 } %** { MUL2 } %// { DIV2 }
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }

|0000

	@src $40
	@token $40
	@scope $40
	@sleep $1
	@s
		&ptr $2
	@r
		&ptr $2
	@p
		&ptr $2
		&len $2

|0100

	;on-console .Console/vector DEO2

BRK

@on-console ( -> )

	.Console/read DEI
	DUP #20 < ,&end JCN
	DUP #7f > ,&end JCN
	;src ;slen JSR2 #003f >> ,&end JCN
		;src ROT ;sput JSR2 BRK
		&end
	POP

	( assemble )

	;src .File/name DEO2
	#0001 .File/length DEO2
	&stream
		;&c
			DUP2 .File/read DEO2
			( comment )
			LDAk #28 ! ,&no-sleep JCN
				#01 .sleep STZ ,&no-cap JMP
				&no-sleep
			LDAk #29 ! ,&no-wake JCN
				#00 .sleep STZ ,&no-cap JMP
				&no-wake
			.sleep LDZ ,&no-cap JCN
			( listen )
			LDAk #21 < ,&no-put JCN
				LDAk ;token ;scap JSR2 STA
				&no-put
			LDAk #20 > ,&no-cap JCN
				;find-token JSR2
				;token #0040 ;mclr JSR2
				&no-cap
		POP2
		.File/success DEI2 ORA ,&stream JCN

	;resolve JSR2

	( make rom )
	;&rom-ext ;src ;scap JSR2 #0003 -- ;scpy JSR2
	;src .File/name DEO2
	.p/len LDZ2 #0100 -- .File/length DEO2
	;dst/clip .File/write DEO2

	#010e DEO

	HALT

BRK
	&c $1
	&rom-ext "rom $1

@find-token ( -- )

	( skip blank ) ;token ;slen JSR2 ORA #01 JCN RTN

	;token LDA
	( pads )
	DUP LIT '| ! ,&no-padabs JCN
		( TODO: use generic shex )
		;token INC2 ;sshort JSR2 .p/ptr STZ2 POP RTN
		&no-padabs
	DUP LIT '$ ! ,&no-padrel JCN
		( TODO: use generic shex )
		#00 ;token INC2 ;sbyte JSR2 .p/ptr STZ2 POP RTN
		&no-padrel
	( labels )
	DUP LIT '@ ! ,&no-label JCN 
		;scope #0040 ;mclr JSR2
		;token INC2 ;scope ;scpy JSR2
		;token INC2 .p/ptr LDZ2 ;create-label JSR2 POP RTN 
		&no-label
	DUP LIT '& ! ,&no-sublabel JCN 
		;token-sublabel JSR2 POP RTN 
		&no-sublabel
	( literals )
	DUP LIT '# ! ,&no-lithex JCN 
		;token-lithex JSR2 POP RTN
		&no-lithex
	DUP LIT '. ! ,&no-litzp JCN
		;token INC2 LIT '. .p/ptr LDZ2 INC2 ;create-reference JSR2
		#ff ;write-litbyte JSR2 POP RTN
		&no-litzp
	DUP LIT ', ! ,&no-litrel JCN
		;token INC2 LIT ', .p/ptr LDZ2 INC2 ;create-reference JSR2
		#ff ;write-litbyte JSR2 POP RTN
		&no-litrel
	DUP LIT '; ! ,&no-litabs JCN
		;token INC2 LIT '; .p/ptr LDZ2 INC2 ;create-reference JSR2
		#ffff ;write-litshort JSR2 POP RTN
		&no-litabs
	DUP LIT ': ! ,&no-rawabs JCN
		;token INC2 LIT ': .p/ptr LDZ2 ;create-reference JSR2
		#ffff ;write-short JSR2 POP RTN
		&no-rawabs
	( ascii )
	DUP LIT '' ! ,&no-rawchr JCN
		;token INC2 LDA ;write-byte JSR2 POP RTN
		&no-rawchr
	DUP LIT '" ! ,&no-rawstr JCN 
		;token-rawstr JSR2 POP RTN 
		&no-rawstr
	( ignore )
	DUP LIT '[ ! ,&no-ignl JCN 
		POP RTN 
		&no-ignl
	DUP LIT '] ! ,&no-ignr JCN 
		POP RTN 
		&no-ignr
	POP
	( special )
	;token ;is-opcode JSR2 #00 = ,&no-opcode JCN
		;token ;find-opcode JSR2 ;write-byte JSR2 RTN
		&no-opcode
	;token ;slen JSR2 #0004 !! ,&no-rawshort JCN
		;token ;sshort JSR2 ;write-short JSR2 RTN
		&no-rawshort
	;token ;slen JSR2 #0002 !! ,&no-rawbyte JCN
		;token ;sbyte JSR2 ;write-byte JSR2 RTN
		&no-rawbyte
	;token ;&unknown-token-txt ;print-error JSR2

RTN
	&unknown-token-txt "unknown-token $1

@resolve ( -- )

	;ref LDA2 ORA JMP RTN
	;ref
	&while
		DUP2 ,resolve-reference JSR
		( eol )
		#0002 ++ ;scap JSR2 INC2 LDA2k ORA ,&while JCN
	POP2

RTN

@resolve-reference ( ref* -- )

	STH2k
	( rune )
	#0002 ++ LDA
	DUP LIT '. ! ,&no-litzp JCN
		STH2kr #0003 ++ ;find-label JSR2 LDA2 STH2kr LDA2 ;dst ++ STA POP POP2r RTN
		&no-litzp
	DUP LIT ', ! ,&no-litrel JCN
		STH2kr #0003 ++ ;find-label JSR2 LDA2
		STH2kr LDA2 -- #0002 -- NIP STH2kr LDA2 ;dst ++ STA POP POP2r RTN
		&no-litrel
	DUP LIT '; ! ,&no-litabs JCN
		STH2kr #0003 ++ ;find-label JSR2 LDA2 STH2kr LDA2 ;dst ++ STA2 POP POP2r RTN
		&no-litabs
	DUP LIT ': ! ,&no-rawabs JCN
		STH2kr #0003 ++ ;find-label JSR2 LDA2 STH2kr LDA2 ;dst ++ STA2 POP POP2r RTN
		&no-rawabs
	POP
	POP2r

RTN

@token-sublabel ( -- )

	;&buf #0040 ;mclr JSR2
	;scope ;&buf ;scpy JSR2
	LIT '/ ;&buf ;scap JSR2 STA
	;token INC2 ;&buf ;scat JSR2
	;&buf .p/ptr LDZ2 ;create-label JSR2

RTN
	&buf $40

@token-lithex ( -- )

	;token INC2 ;slen JSR2 #0004 !! ,&no-short JCN
		;token INC2 ;sshort JSR2 ;write-litshort JSR2 RTN
		&no-short
	;token INC2 ;slen JSR2 #0002 !! ,&no-byte JCN
		;token INC2 ;sbyte JSR2 ;write-litbyte JSR2 RTN
		&no-byte
	;token ;&error-invalid-hex ;print-error JSR2

RTN
	&error-invalid-hex "invalid-lithex $1

@token-rawstr ( -- )

	;token INC2
	&while
		LDAk ;write-byte JSR2
		INC2 LDAk ,&while JCN
	POP2

RTN

@create-label ( name* addr* -- )

	( write ref )
	;sym .s/ptr LDZ2 ++ STA2
	.s/ptr LDZ2k #0002 ++ ROT STZ2
	( write string )
	DUP2 ;slen JSR2 STH2
	;sym .s/ptr LDZ2 ++ ;scpy JSR2
	.s/ptr LDZ2k STH2r ++ ROT STZ2
	( null term )
	#00 ;sym .s/ptr LDZ2 ++ STA
	.s/ptr LDZ2k INC2 ROT STZ2

RTN

@create-reference ( name* rune addr* -- )

	( addr )
	;ref .r/ptr LDZ2 ++ STA2
	.r/ptr LDZ2k #0002 ++ ROT STZ2
	( rune )
	;ref .r/ptr LDZ2 ++ STA
	.r/ptr LDZ2k INC2 ROT STZ2

	LDAk LIT '& ! ,&no-sublabel JCN
		;&buf #0040 ;mclr JSR2
		;scope ;&buf ;scpy JSR2
		LIT '/ ;&buf ;scap JSR2 STA
		INC2 ;&buf ;scat JSR2
		;&buf
		&no-sublabel

	( write string )
	DUP2 ;slen JSR2 STH2
	;ref .r/ptr LDZ2 ++ ;scpy JSR2
	.r/ptr LDZ2k STH2r ++ ROT STZ2
	( null term )
	#00 ;ref .r/ptr LDZ2 ++ STA
	.r/ptr LDZ2k INC2 ROT STZ2

RTN
	&buf $40

@find-label ( name* -- addr* )

	STH2
	;sym
	&while
		DUP2 #0002 ++ STH2kr ;scmp JSR2 #00 = ,&continue JCN
			POP2r RTN
			&continue
		( eol )
		#0002 ++ ;scap JSR2 INC2 LDA2k ORA ,&while JCN
	POP2
	STH2r ;&error-invalid-label ;print-error JSR2
	#0000

RTN
	&error-invalid-label "invalid-label $1

@write-litbyte ( byte -- )

	( LITk ) #80 ,write-byte JSR ,write-byte JSR

RTN

@write-litshort ( short* -- )

	( LIT2k ) #a0 ,write-byte JSR

@write-short ( short -- )

	SWP ,write-byte JSR

@write-byte ( byte -- )

	;dst .p/ptr LDZ2 STH2k ++ STA
	INC2r STH2kr .p/ptr STZ2
	STH2r .p/len STZ2

RTN

@opcodes
	"LIT $1 "INC $1 "POP $1 "DUP $1 "NIP $1 "SWP $1 "OVR $1 "ROT $1
	"EQU $1 "NEQ $1 "GTH $1 "LTH $1 "JMP $1 "JCN $1 "JSR $1 "STH $1
	"LDZ $1 "STZ $1 "LDR $1 "STR $1 "LDA $1 "STA $1 "DEI $1 "DEO $1
	"ADD $1 "SUB $1 "MUL $1 "DIV $1 "AND $1 "ORA $1 "EOR $1 "SFT $1

@find-opcode ( name* -- byte )

	STH2
	#2000
	&loop
		#00 OVR #20 SFT2 ;opcodes ++ STH2kr ;scmp-seg JSR2 ,&found JCN
		INC GTHk ,&loop JCN
	POP2
	POP2r
	#00

RTN
	&found NIP STH2r ,find-mode JSR + RTN ( TODO: rewrite, STH2r unsafe )

@find-mode ( mode* -- byte )

	LITr 00
	;opcodes OVR2 ;scmp-seg JSR2 #70 SFT STH ADDr
	#0003 ++
	LDAk #00 = ,&end JCN
	&while
		LDAk LIT '2 = #50 SFT STH ADDr
		LDAk LIT 'r = #60 SFT STH ADDr
		LDAk LIT 'k = #70 SFT STH ADDr
		INC2 LDAk ,&while JCN
	&end
	POP2
	STHr

RTN

@is-opcode ( string* -- bool )

	DUP2 ;&brk-txt ;scmp JSR2 #00 = ,&continue JCN
		POP2 #01 RTN
		&continue
	;find-opcode JSR2

RTN
	&brk-txt "BRK $1

@sshort ( str* -- short* )

	INC2k INC2 ,sbyte JSR STH
	,sbyte JSR STHr

RTN

@sbyte ( str* -- byte )

	LDAk ,chex JSR STH
	INC2 LDA ,chex JSR
	STHr #40 SFT ADD

RTN

@chex ( char -- hex )

	DUP #2f > OVR #3a < AND ,&number JCN
	DUP #60 > OVR #67 < AND ,&lc JCN
	DUP #40 > OVR #47 < AND ,&uc JCN
		POP #00 RTN
	&number #30 - RTN
	&uc #37 - RTN
	&lc #57 - RTN

RTN

( helpers )

@mclr ( addr* len* -- )

	OVR2 ++ SWP2
	&loop
		STH2k #00 STH2r STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@slen ( str* -- len* )

	DUP2 ,scap JSR SWP2 --

RTN

@scap ( str* -- str-end* )

	LDAk #00 ! JMP RTN
	&while INC2 LDAk ,&while JCN

RTN

@scat ( src* dst* -- )

	DUP2 ,slen JSR ++ ,scpy JSR

JMP2r

@scpy ( src* dst* -- )
	
	STH2
	&while
		LDAk STH2kr STA INC2r
		INC2 LDAk ,&while JCN
	POP2
	#00 STH2r STA

JMP2r

@sput ( str* char -- )

	ROT ROT ,scap JSR STA

RTN

@scmp-seg ( a* b* -- flag )

	STH2
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 JMP2r
			&not-diff
		INC2k LDA #00 ! ,&continue JCN
			POP2 POP2r #01 JMP2r
			&continue
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

JMP2r

@scmp ( a* b* -- flag )

	STH2
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 JMP2r
			&not-diff
		LDAk LDAkr STHr ORA ,&not-end JCN
			POP2 POP2r #01 JMP2r
			&not-end
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

JMP2r

@print ( short* -- )

	&short ( short* -- ) SWP ,&byte JSR
	&byte ( byte -- ) DUP #04 SFT ,&char JSR
	&char ( char -- ) #0f AND DUP #09 GTH #27 MUL ADD #30 ADD EMIT

RTN

@print-error ( token* error* -- )

	SWP2 ;print-str JSR2 #20 EMIT

@print-str ( str* -- )

	#0001 SUB2
	&while
		INC2 LDAk DUP EMIT ,&while JCN
	POP2
	#0a EMIT

RTN

( buffers )
@sym $4000 @ref $4000 @dst $0100 &clip
