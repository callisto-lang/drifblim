( build: uxnasm src/drif.tal drif.rom
| start: uxnemu drif.rom )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1
|20 @Screen/vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller/vector $2 &button $1 &key $1
|90 @Mouse/vector $2 &x $2 &y $2 &state $1 &pad $3 &sx $2 &sy $1 &sy-lb $1
|a0 @File/vector $2 &success $1 &success-lb $1 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

(
@|enums )

|00 @RefType &ab $1 &as $1 &rb $1 &rs $1
|00 @SymType $40 &used $40 &declared
|0012 @header/height

|000

	@src/buf $2f &cap $1
	@dst/buf $2f &cap $1
	@token/buf $2f &cap $1
	@scope/buf $2f &cap $1

|100

@on-reset ( -> )
	;meta #06 DEO2
	theme/<load>
	#0180 #00e8 window/<set-size>
	;dict/reset scope/<set>
	;src/on-console .Console/vector DEO2
	<redraw-all>
	;on-mouse .Mouse/vector DEO2
	BRK

@on-mouse ( -> )
	cursor/<update>
	.Mouse/state DEI ?{ BRK }
	&check-hit ( -> )
	.Mouse/x DEI2 .Mouse/y DEI2 src/hit dst/hit assemble/hit POP2 POP2 BRK

@meta $1
	( name ) "Drif 0a
	( desc ) "Uxntal 20 "Assembler 0a
	( auth ) "By 20 "Devine 20 "Lu 20 "Linvega 0a
	( date ) "16 20 "Mar 20 "2025 $2

(
@|src )

@src/on-console ( -> )
	[ LIT2 02 -Console/type ] DEI LTH ?{ .Console/read DEI !/on-push }
	dst/<trap>
	BRK

@src/on-mouse ( -> )
	cursor/<update-trap>
	.Mouse/state DEI ?on-mouse/check-hit
	BRK

@src/on-button ( -> )
	.Controller/key DEI
	( >> )

@src/on-push ( c -> )
	DUP ?{ POP BRK }
	DUP #0d EQU ?/on-done
	DUP #08 EQU ?/on-pop
	[ LIT2 -&cap &ptr -&buf ]
	( clamp ) NEQk ?{ POP POP2 BRK }
	INCk ,&ptr STR
	NIP STZ
	/<draw>
	BRK

@src/on-pop ( c -> )
	POP #00 [ LIT2 -&buf _&ptr ] LDR
	( clamp ) NEQk ?{ POP2 BRK }
	#01 SUB DUP ,&ptr STR
	NIP STZ
	/<draw>
	BRK

@src/on-done ( c -> )
	POP dst/<trap>
	BRK

@src/inactive ( -- f )
	;&on-mouse .Mouse/vector DEI2 NEQ2 JMP2r

@src/hit ( x* y* -- x* y* )
	DUP2 ,&y LDR2 #0014 ADD2 SUB2 #0014 GTH2 ?{
		OVR2 ,&x LDR2 #0006 ADD2 SUB2 #00c8 GTH2 ?{ /<trap> } }
	JMP2r

@src/<trap> ( -- )
	/inactive ?{ JMP2r }
	;&on-console .Console/vector DEO2
	;&on-mouse .Mouse/vector DEO2
	;&on-button .Controller/vector DEO2 !<redraw>

@src/<draw> ( -- )
	[ LIT2 &x 0010 ] .Screen/x DEO2
	[ LIT2 &y 001c ] .Screen/y DEO2
	/inactive ?{
		;&buf ;dict/file-a #01 field/<draw-color>
		[ LIT "| ] #01 !chicago/<draw-char-color> }
	.&buf ;&ptr LDA EQU ?{ ;&buf ;dict/file-a #01 !field/<draw-color> }
	;&placeholder ;dict/file-a #02 !field/<draw-color>

	&placeholder "source.tal $1

(
@|dst )

@dst/on-console ( -> )
	[ LIT2 02 -Console/type ] DEI LTH ?{ .Console/read DEI !/on-push }
	asm/<trap>
	BRK

@dst/on-mouse ( -> )
	cursor/<update-trap>
	.Mouse/state DEI ?on-mouse/check-hit
	BRK

@dst/on-button ( -> )
	.Controller/key DEI
	( >> )

@dst/on-push ( c -> )
	DUP ?{ POP BRK }
	DUP #0d EQU ?/on-done
	DUP #08 EQU ?/on-pop
	[ LIT2 -&cap &ptr -&buf ]
	( clamp ) NEQk ?{ POP POP2 BRK }
	INCk ,&ptr STR
	NIP STZ
	/<draw>
	BRK

@dst/on-pop ( c -> )
	POP #00 [ LIT2 -&buf _&ptr ] LDR
	( clamp ) NEQk ?{ POP2 BRK }
	#01 SUB DUP ,&ptr STR
	NIP STZ
	/<draw>
	BRK

@dst/on-done ( c -> )
	POP asm/<trap>
	BRK

@dst/inactive ( -- f )
	;&on-mouse .Mouse/vector DEI2 NEQ2 JMP2r

@dst/hit ( x* y* -- x* y* )
	DUP2 ,&y LDR2 #0014 ADD2 SUB2 #0014 GTH2 ?{
		OVR2 ,&x LDR2 #0006 ADD2 SUB2 #00c8 GTH2 ?{ /<trap> } }
	JMP2r

@dst/<trap> ( -- )
	/inactive ?{ JMP2r }
	;&on-console .Console/vector DEO2
	;&on-mouse .Mouse/vector DEO2
	;&on-button .Controller/vector DEO2 !<redraw>

@dst/<draw> ( -- )
	[ LIT2 &x 0010 ] .Screen/x DEO2
	[ LIT2 &y 0050 ] .Screen/y DEO2
	/inactive ?{
		;&buf ;dict/file-a #01 field/<draw-color>
		[ LIT "| ] #01 !chicago/<draw-char-color> }
	.&buf ;&ptr LDA EQU ?{ ;&buf ;dict/file-a #01 !field/<draw-color> }
	;&placeholder ;dict/file-a #02 !field/<draw-color>

	&placeholder "result.rom $1

(
@|assemble )

@asm/<trap> ( -- )
	JMP2r

@assemble/hit ( x* y* -- x* y* )
	JMP2r

@lib/inctal INC2
	( >> )

@<handle-file> ( f* -- )
	.File/name DEO2
	#0001 .File/length DEO2
	token/<new>
	#0000
	&>s ( len* -- )
		.System/state DEI ?&end
		;&c .File/read DEO2
		.File/success-lb DEI ?{
			ORAk ?{ ;dict/invalid ;src/buf ;dict/File <set-error> }
			&end ( i* -- )
			POP2 JMP2r }
		INC2 [ LIT &c $1 ] token/<push-byte> !&>s

@rom/<emit> ( length* -- )
	;dict/assembled <pstr>
	#2019 DEO
	;dst/buf <pstr>
	;dict/in <pstr>
	[ LIT2 &length 0100 ] DUP2 #0100 SUB2 <pdec>
	;dict/bytes <pstr>
	( | write )
	;dst/buf .File/name DEO2
	#0100 SUB2 .File/length DEO2
	;&output .File/write DEO2
	JMP2r

@runes/lut [
	"| =lib/padabs "$ =lib/padrel
	"@ =lib/toplab "& =lib/sublab
	"% =lib/macros "( =lib/coment
	", =lib/litrel "_ =lib/rawrel
	". =lib/litzep "- =lib/rawzep
	"; =lib/litabs "= =lib/rawabs
	"? =lib/litjci "! =lib/litjmi
	"# =lib/lithex "" =lib/rawstr
	"} =lib/lambda "~ =lib/inctal
	5b =lib/ignore 5d =lib/ignore ] $1

@dict/File "File $1

@dict/drifloon "Drifloon $1

@dict/assemble "Assemble $1

@dict/file-a "Read 20 "source 20 "file: $1

@dict/file-b "Write 20 "output 20 "file: $1

@dict/space-left "49212 20 "bytes 20 "free. $1

@dict/warnings "Ready. $1

@<redraw-all> ( -- )
	header/<draw>
	( >> )

@<redraw> ( -- )
	src/<draw>
	dst/<draw>
	#0010 .Screen/x DEO2
	#0088 .Screen/y DEO2
	;dict/assemble button/<draw>
	( | labels )
	#00a8 .Screen/x DEO2
	#0090 .Screen/y DEO2
	;dict/space-left #01 chicago/<draw-left-color>
	( | line )
	#0000 .Screen/x DEO2
	#00b8 .Screen/y DEO2
	#01d0 ;dashhor-icn #01 <draw-times>
	( | warnings )
	#0010 .Screen/x DEO2
	#00c8 .Screen/y DEO2
	;dict/warnings #01 chicago/<draw-left-color>
	JMP2r

@dashhor-icn [ ee00 0000 0000 0000 ]

@next-glyph ( addr* -- addr* )
	INC2 LDAk
	( utf8 ) #06 SFT #02 EQU ?next-glyph
	JMP2r

@header/<draw-title> ( -- )
	.Screen/width DEI2 #01 SFT2 .Screen/x DEO2
	#0001 .Screen/y DEO2
	#09 ;chicago/color STA
	[ LIT2 15 -Screen/auto ] DEO
	.Screen/x DEI2 ;dict/drifloon chicago/get-str-width #01 SFT2 SUB2 .Screen/x DEO2
	#20 chicago/<draw-char>
	;dict/drifloon chicago/<draw-left>
	#20 chicago/<draw-char>
	( | clip )
	.Screen/y DEI2k #0001 SUB2 ROT DEO2
	;header/bg-chr .Screen/addr DEO2k [ LIT2r 81 -Screen/sprite ] DEOkr
	DEO2
	DEOr
	JMP2r

@field/<draw-color> ( src* color -- )
	,&color STR
	( >> )

@field/<draw> ( src* label* -- )
	[ LITr -Screen/x ] DEI2r #01 chicago/<draw-left-color>
	[ LITr -Screen/x ] DEO2r
	.Screen/y DEI2k #0010 ADD2 ROT DEO2
	( | frame )
	[ LIT2 35 -Screen/auto ] DEO
	;&edge-icn .Screen/addr DEO2
	[ LIT2 01 -Screen/sprite ] DEO
	#01e8 ;&core-icn #35 <draw-times-addr>
	;&edge-icn .Screen/addr DEO2
	[ LIT2 11 -Screen/sprite ] DEO
	( | content )
	.Screen/x DEI2k #00bb SUB2 ROT DEO2
	.Screen/y DEI2k #0008 ADD2 ROT DEO2
	[ LIT &color $1 ] chicago/<draw-left-color>
	JMP2r

@field
	&edge-icn [
	0000 0000 3f7f 6060 6060 6060 6060 6060
	6060 6060 6060 6060 6060 7f3f 0000 0000 ]
	&core-icn [
	0000 0000 ffff 0000 0000 0000 0000 0000
	0000 0000 0000 0000 0000 ffff 0000 0000 ]

@button/<draw> ( name* -- )
	[ LIT2 35 -Screen/auto ] DEO
	;&edge-icn .Screen/addr DEO2
	[ LIT2 01 -Screen/sprite ] DEO
	#01f0 ;&core-icn #35 <draw-times-addr>
	;&edge-icn .Screen/addr DEO2
	[ LIT2 11 -Screen/sprite ] DEO
	( | content )
	.Screen/x DEI2k #0038 SUB2 ROT DEO2
	.Screen/y DEI2k #0008 ADD2 ROT DEO2
	#01 chicago/<draw-center-color>
	JMP2r

@button
	&edge-icn [
	0000 0000 1f20 4f50 5050 5050 5050 5050
	5050 5050 5050 5050 504f 201f 0000 0000 ]
	&core-icn [
	0000 0000 ff00 ff00 0000 0000 0000 0000
	0000 0000 0000 0000 00ff 00ff 0000 0000 ]

@<phex> ( short* -: )
	SWP /b
	&b ( byte -: )
	DUP #04 SFT /c
	&c ( byte -: )
	#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
	JMP2r

~src/drif.util.tal

