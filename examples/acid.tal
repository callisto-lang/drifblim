( test file )

( padding prelude )

|0 @padabs
|f &p1
|0f &p2
|00f &p3
|000f &p4

|0 @padrel
$4 &p1
$4 &p2
$4 &p3
$4 &p4

|0 @padlab
|padrel/p1 &p1
$padrel/p1 &p2
|/p1 &p3
$/p2 &p4

( start )

|0100 @program

	padabs/<test>
	padrel/<test>
	padlab/<test>


	#01 ?{ ( skip ) #ffff }
	!{ ( skip ) #ffff }
	{ ( skip ) #ffff } POP2r

	( nested lambda )
	{ { "hello 0a $1 } STH2r !print-str } STH2r JSR2

	( function application )
	{ 01 02 03 04 05 } STH2r { LIT "0 ADD #18 DEO #2018 DEO JMP2r } STH2r foreach

	( get lambda length )
	{ "Dindeldums $1 } STH2r get-lambda-length <print-dec> #2018 DEO

	( allocated string )
	;hello-word print-str

	#800f DEO

BRK

(
@|Tests )

@padabs/<test> ( -- )
	.&p1 .&p2 EQU
	;&p3 ;&p4 EQU2
		AND
	.&p1 #0f EQU
		AND ;&name !<validate>
	&name "padabs 20 $1

@padrel/<test> ( -- )
	;&p1 ;&p2 ADD
	;&p3 ;&p4 ADD
		#10 EQU
	.&p1 #04 EQU
		AND ;&name !<validate>
	&name "padrel 20 $1

@padlab/<test> ( -- )
	.&p1 .&p3 EQU
	;&p4 ;&p2 SUB2 ;&p1 EQU2
		AND ;&name !<validate>
	&name "padrel 20 $1

(
@|Utils )

@<validate> ( flag name* -- )
	<pstr>
	?{ ;&fail !<pstr> }
	;&pass !<pstr>
	&fail "fail 0a $1
	&pass "pass 0a $1

@<pstr> ( str* -- )
	LDAk DUP ?{ POP POP2 JMP2r }
	#18 DEO INC2 !<pstr>

@<phex> ( short* -: )
	SWP /b
	&b ( byte -: )
		DUP #04 SFT /c
	&c ( byte -: )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r

( forward references )

.forward
;forward
,forward
!forward
@forward

( backward references )

@backward
.backward
;backward
,backward
!backward

(
@| test label inheritance )

@Object &x $1 &y $1

&get-x ( -- x )
	,&x LDR
	JMP2r

@Object/get-y ( -- y )
	,&y LDR
	JMP2r

@Object/get-both ( -- x y )
	/get-x /get-y
	JMP2r

( raw lambda length )
_{ 01 02 03 }

@get-lambda-length ( lambda* -- length* )
	#0002 SUB2 LDA2
JMP2r

@print-str ( str* -- )
	&while ( -- )
		( send ) LDAk #18 DEO
		( loop ) INC2 LDAk ?&while
	POP2

JMP2r

@foreach ( arr* fn* -- )
	STH2
	DUP2
	DUP2 #0002 SUB2 LDA2 ADD2
	SWP2
	&l
		LDAk STH2kr JSR2
		INC2 NEQ2k ?&l
	POP2 POP2 POP2r
JMP2r

( a ( nested ( comment ) ) )

@<print-dec> ( short* -- )
	#2710 [ LIT2r 00fb ]
	&w ( -- )
		DIV2k #000a DIV2k MUL2 SUB2 SWPr EQUk OVR STHkr EQU AND ?&>skip
			DUP [ LIT "0 ] ADD #19 DEO
			INCr &>skip
		POP2 #000a DIV2 SWPr INCr STHkr ?&w
	POP2r POP2 POP2 JMP2r

$20 @label2

@hello-word "Hello 20 "World! 0a $1


